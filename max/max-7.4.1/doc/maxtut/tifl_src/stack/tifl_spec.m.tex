\subsection{tifl\_spec.m}
\begin{verbatim}

////////////////////////////////////////////////////////////////
//   Abstract Syntax of TIFL 
/////////////////////////////////


Program      (  Exp  )
Exp          =  LetExp  |  Int  |  Bool  |  CondExp  |  FctAppl  |  UsedId
LetExp       (  FctDeclList  Exp  )
FctDeclList  *  FctDecl
FctDecl      (  Type  DeclId  ParDeclList  Body  )
Type         =  Boolean  |  Integer
Boolean      ()
Integer      ()
ParDeclList  *  ParDecl
ParDecl      (  Type  DeclId  )
Body         =  Exp  |  PredeclBody
PredeclBody  () 
CondExp      (  Exp  Exp  Exp  )
FctAppl      (  UsedId  ExpList  )
ExpList      *  Exp
UsedId       (  Ident  LineNo  )
DeclId       (  Ident  LineNo  )


Decl         =  FctDecl |  ParDecl
Scope        =  FctDecl |  LetExp
LineNo       =  Int


/////////////////////////////////////////////////////////////
//   some sorts for the evaluator
/////////////////////////////////
Value        =  Bool | Int


//////////////////////////////
//  function declarations  
//  (bodies in tifl.c)
//////////////////////////////

FCT itos( Int ) String

FCT and( Bool, Bool ) Bool
FCT or( Bool, Bool ) Bool
FCT not( Bool ) Bool

FCT add( Int, Int ) Int
FCT sub( Int, Int ) Int
FCT mul( Int, Int ) Int

FCT lt( Int, Int ) Bool
FCT le( Int, Int ) Bool
FCT equ( Int, Int ) Bool


STRUC  tifl  [ Program ]{

FCT id( Node I ) Ident : term( son( 1, I ) )
FCT ln( Node N ) String : itos( term( son( -1, N ) ) )

ATT encl_scope( Node N ) Scope@ :
   IF  is[ N, _FctDecl@ ]            : N
   |   is[ N, _LetExp@ ]             : N 
   ELSE                     encl_scope( fath( N ) )

FCT first_decl( Scope@ S ) Decl@:
   IF  LetExp@<<FCTD,*>,_> S     :   FCTD
   |   FctDecl@<_,_,<PARD,*>,_> S:   PARD
   ELSE nil()

FCT lookup_list( Ident ID, Decl@ D ) DeclId@ :
   IF  ID = id( son(2,D) )  :  son(2,D)   ELSE  lookup_list( ID, rbroth(D) )

FCT lookup( Ident ID, Scope@ S ) DeclId@ :
   LET  DID == lookup_list(ID,first_decl(S)) :
   IF   DID # nil() :  DID  ELSE  lookup( ID, encl_scope(fath(S)) )

ATT decl( UsedId@ UID ) DeclId@ :
   lookup( id(UID), encl_scope(UID) )

ATT type( Exp@ E ) Type :
   IF LetExp@<_,EX> E   :  type( EX )
   |  Int@ E            :  Integer()
   |  Bool@ E      :  Boolean()
   |  CondExp@<_,E2,_> E:  type( E2 ) 
   |  FctAppl@<UID,_> E :  type( UID ) 
   |  UsedId@ E         :  term( son( 1, fath(decl(E)) ) )
   ELSE nil()


PRD par_type_check[ ParDecl@ P, Exp@ E ]:
   term(son(1,P)) = type(E)
 && { rbroth(P) # nil()  ->  par_type_check[ rbroth(P), rbroth(E) ] }
   

////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////
//
//     Context Conditions 
//
//////////////////////////////////////////////////////////////////

CND CondExp@<_,E1,E2>               : type(E1) = type(E2)
| `"### : then/else expr's must be of same type\n"'


CND CondExp@<E1,*>                  : is[type(E1),_Boolean] 
| `"### : conditional expr must be of type Boolean\n"'


CND ParDeclList@<*,<_,ID1>,*,<_,ID2>,*>  : id(ID1) # id(ID2)
| `"### LINE(S) " ln(ID1) "/" ln(ID2) ": parameter \"" idtos(id(ID1))
  "\" doubly declared\n"'


CND FctDeclList@<*,<_,ID1,*>,*,<_,ID2,*>,*>   : id(ID1) # id(ID2)
| `"### LINE(S) " ln(ID1) "/" ln(ID2) ": function \"" idtos(id(ID1))
 "\" doubly declared\n"'
 

CND UsedId@ UID                   : decl(UID) # nil()
| `"### LINE " ln(UID) ": identifier \"" idtos(id(UID)) "\" not declared\n"'


CND FctAppl@<UID,EL>     : numsons(EL) = numsons( son( 3, fath( decl(UID) ) ) )
| `"### LINE " ln(UID) ": incorrect number of parameters in call of function "
  idtos(id(UID))"\n"'


CND FctAppl@<UID,<E,*>>      :  par_type_check[ son(1,son(3,fath(decl(UID)))), E ]
| `"### LINE " ln(UID) ": type mismatch in call of function "idtos(id(UID))"\n"'


CND FctDecl@<T,DID,_,B>                :
 !is[ B , _PredeclBody@] -> term( T ) = type( B )
| `"### LINE " ln(DID) ": declaration of "idtos(id(DID))
  " does not match function result\n"'


CND UsedId@ UID                        :
   rbroth(UID) = nil() -> !is[ fath( decl ( UID ) ), _FctDecl@ ]
| `"### LINE " ln(UID) ": incorrect call of "idtos(id(UID))" \n"'


///////////////////////////////////////////////////////////////////////////
//
//  eval and related functions
//  (stack-version)
///////////////////////////////////////////////////////////////////////////
                         

FCT  par_lookup( ParDecl@ ) Value
//   returns top of stack for specified parameter

FCT  eval_userfunc( ParDecl@, Exp@, Exp@ ) Value
//   
//   prolog (-> push_params), evaluation of function body, epilog (->pop_params)

FCT  eval_predecl( Ident ID, Exp@ EN ) Value :
   IF  idtos(ID) = "or"      :   or( eval(EN), eval(rbroth(EN)) )
    |  idtos(ID) = "and"     :   and( eval(EN), eval(rbroth(EN)) )
    |  idtos(ID) = "not"     :   not( eval(EN) )
    |  idtos(ID) = "add"     :   add( eval(EN), eval(rbroth(EN)) )
    |  idtos(ID) = "sub"     :   sub( eval(EN), eval(rbroth(EN)) )
    |  idtos(ID) = "mul"     :   mul( eval(EN), eval(rbroth(EN)) )
    |  idtos(ID) = "lt"      :   lt( eval(EN), eval(rbroth(EN)) )
    |  idtos(ID) = "le"      :   le( eval(EN), eval(rbroth(EN)) )
    |  idtos(ID) = "equ"     :   equ( eval(EN), eval(rbroth(EN)) )
   ELSE  nil()

FCT  eval ( Exp@ X ) Value:
   IF  LetExp@<_,BD>        X:  eval( BD )
    |  Int@                 X:  term(X)
    |  Bool@                X:  term(X)
    |  CondExp@<E1,E2,E3>   X:  IF eval(E1) = true() : eval(E2)  ELSE  eval(E3) 
    |  UsedId@              X:  par_lookup( fath(decl(X)) )
    |  FctAppl@<UID,<>>     X:  eval( son(4, UID.decl.fath ) )
    |  FctAppl@<UID,<E1,*>> X:  LET  FCTDCL ==  UID.decl.fath  :
   IF  FctDecl@<_,<IDN,_>,_,PredeclBody@> FCTDCL:  eval_predecl( term(IDN), E1 )
   ELSE   eval_userfunc( son(1,son(3,FCTDCL)), E1, son(4,FCTDCL) )
   ELSE nil()

}

\end{verbatim}

